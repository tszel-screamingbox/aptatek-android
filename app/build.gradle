buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

apply from: "${project.rootDir}/dependencies.gradle"
apply from: "${project.rootDir}/signing/signing.gradle"
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

jacoco {
    toolVersion = "${versions.jacoco}"
}

checkstyle {
    toolVersion "${versions.checkstyle}"
}

findbugs {
    toolVersion "${versions.findbugs}"
}

android {
    compileSdkVersion versions.targetsdk
    buildToolsVersion versions.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.aptatek.pkuapp"
        minSdkVersion versions.minsdk
        targetSdkVersion versions.targetsdk
        vectorDrawables.useSupportLibrary = true
        versionCode 11
        versionName "0.0.11"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
        testInstrumentationRunnerArguments clearPackageData: 'true'
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false // configure proguard and enable
            shrinkResources false // configure proguard and enable
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            multiDexEnabled true
        }
    }

    flavorDimensions "mock"

    productFlavors {

        mock {
            applicationIdSuffix "mock"
            versionNameSuffix "-mock"
            dimension "mock"
        }

        prod {

        }

    }

    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'RestrictedApi'
    }
}

checkstyle {
    configFile rootProject.file('quality/checkstyle.xml')
    showViolations true
}

dependencies {
    // Support
    implementation androidSupportDependencies.appcompat
    implementation androidSupportDependencies.annotations
    implementation androidSupportDependencies.recyclerview
    implementation androidSupportDependencies.constraintLayout
    implementation androidSupportDependencies.design
    implementation androidSupportDependencies.multidex

    // ArchitectureComponents
    implementation architectureComponentsDependencies.extensions
    annotationProcessor architectureComponentsDependencies.compiler

    // MVP
    implementation mvpDependencies.mosby
    implementation mvpDependencies.viewstate

    // View binding
    implementation butterknifeDependencies.butterknife
    annotationProcessor butterknifeDependencies.compiler

    // RX
    implementation rxDependencies.rxjava
    implementation rxDependencies.rxandroid
    implementation rxDependencies.ixjava

    // Dagger
    implementation daggerDependencies.dagger
    annotationProcessor daggerDependencies.compiler
    compileOnly daggerDependencies.annotations

    // Database
    implementation databaseDependencies.room
    annotationProcessor databaseDependencies.compiler
    implementation databaseDependencies.rxjava
    implementation databaseDependencies.sqlcipher

    // Logger framework
    implementation appDependencies.timber

    // Crashlytics
    prodImplementation crashlyticsDependencies.crashlytics

    // AutoValue
    implementation autoValueDependencies.api
    annotationProcessor autoValueDependencies.compiler
    annotationProcessor autoValueDependencies.parceler

    // IntentBuilder
    implementation activityStarterDependencies.activityStarter
    annotationProcessor activityStarterDependencies.apt

    // UI
    implementation uiDependencies.autoFitTextView
    implementation(uiDependencies.rangebar) {
        exclude module: 'support-compat'
    }
    implementation uiDependencies.pagerIndicator
    implementation uiDependencies.frameVideoView

    // LeakCanary
    debugImplementation leakDependencies.debug
    releaseImplementation leakDependencies.release
    testImplementation leakDependencies.release

    // DiscrateScrollView
    implementation scrollView.discreteScrollView

    // MpAndroidChart
    implementation mpAndroidChartDependencies.androidChart

    // Time
    implementation timeDependencies.yodatime

    // Bluetooth
    implementation bluetoothDependencies.nordicBle

    // Tests
    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
    androidTestImplementation androidSupportDependencies.multidex
    testImplementation databaseDependencies.testhelper
    androidTestImplementation testDependencies.testrunner
    androidTestImplementation testDependencies.testrules
    androidTestImplementation testDependencies.hamcrest
    androidTestImplementation(testDependencies.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile(testDependencies.espressocontrib) {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
    }
    androidTestAnnotationProcessor daggerDependencies.compiler
    androidTestCompileOnly daggerDependencies.annotations
    implementation appDependencies.dataFactory
    // TODO temporarily added to PROD flavor, only for the fake cube data generation.
    androidTestUtil testDependencies.orchestrator

}

configurations.all {
    resolutionStrategy {
        force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

task customCheckstyle(type: Checkstyle, dependsOn: 'assembleProdDebug') {
    description = 'Running Checkstyle'
    group = 'verification'
    ignoreFailures = true
    showViolations = true

    configFile file("$rootProject.rootDir/quality/checkstyle.xml")

    source 'src/main/java', 'src/prod/java'
    include '**/*.java'
    exclude '**/*Test.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    reports {
        xml.enabled = false
        html.enabled = true
        xml.destination file("$project.buildDir/reports/checkstyle/checkstyle-output.xml")
        html.destination file("$project.buildDir/reports/checkstyle/checkstyle-output.html")
    }

    classpath = files()
}

task customFindBugs(type: FindBugs, dependsOn: 'assembleProdDebug') {
    description = 'Runing FindBugs'
    group = 'verification'
    effort = 'max'
    reportLevel = 'medium'
    source 'src/main/java', 'src/prod/java'
    classes = fileTree('build/intermediates/classes/prod/debug')
    classpath = files()
    ignoreFailures = true

    // Use this only if you want exclude some errors
    excludeFilter = file("$rootProject.rootDir/quality/findbugs-exclude.xml")

    reports {
        xml.enabled = false
        xml.withMessages = true
        html.enabled = true
        xml.destination file("$project.buildDir/reports/findbugs/findbugs-output.xml")
        html.destination file("$project.buildDir/reports/findbugs/findbugs-output.html")
    }
}

task customPmd(type: Pmd, dependsOn: 'assembleProdDebug') {
    ruleSetFiles = files("$rootProject.rootDir/quality/pmd.xml")
    ignoreFailures = false
    ruleSets = []

    source 'src/main/java', 'src/prod/java'
    include '**/*.java'
    exclude '**/*Test.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination file("$project.buildDir/reports/pmd/pmd.html")
        }
    }
}

def coverageSourceDirs = [
        'src/main/java',
        'src/prod/java'
]

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testProdDebugUnitTest') {
    description = 'Generate JaCoCo coverage reports'
    group = 'Reporting'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/prod/debug',
            excludes: [
                    '**/R.class',
                    '**/R$*.class',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/AutoValue_*.*',
                    '**/Dagger*.*',
                    '**/*Module_Provide*Factory.*',
                    '**/*_Factory.*',
                    '**/*_MembersInjector.*',
                    '**/*_ViewBinding*',
                    '**/*_LifecycleAdapter.*',
                    '**/base/**',
                    '**/injection/**'
            ]
    )

    sourceDirectories = files(coverageSourceDirs)
    executionData = fileTree(
            dir: "$buildDir",
            include: ['jacoco/testProdDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec', '../../firebase/*coverage.ec']
    )

    doFirst {
        files('build/intermediates/classes/prod/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

android.variantFilter { variant ->
    if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
        variant.setIgnore(true)
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

apply plugin: 'com.google.gms.google-services'
